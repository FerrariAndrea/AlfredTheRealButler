System realrobot
mqttBroker "localhost" : 1883

Dispatch local_modelChanged   : modelChanged( TARGET, VALUE )
Dispatch onestep       : onestep(DURATION)
Dispatch stepOk        : stepOk(V)		//TODO eliminate  V
Dispatch stepFail      : stepFail(R,T) //R=ok | obstacle, T = time
Event  sonarRobot     : sonar( DISTANCE )	     //from  sonar on robot 

Context ctxController ip[host="localhost"port=8083] -mqtt
Context ctxDummy ip[host="dummyhost" port=8084] -mqtt //as ctxRobot
//ATTENZIONE:
//viene usata direttamente la mindrobot e non la reourcesmodel
//per semplicità
ExternalQActor mindrobot context ctxDummy


QActor controller context ctxController {
["
	
	var Tback      = 0L
	var StepTime   = 350L	//for real
	var RotateTime = 300L	//for real
	var PauseTime  = 250L 
"]	
	State s0 initial {
		/* 
		forward mindrobot -m local_modelChanged : modelChanged(  robot, w)
		delay 1000
		forward mindrobot -m local_modelChanged : modelChanged(  robot, h)
		*/
		delay 1000
	}
	Goto oneStep
	
	State next{
		
	}
	State oneStep{
			forward onecellforward -m onestep : onestep($StepTime)	
	}
	Transition t0 	whenMsg stepOk   -> handleStepOk   
					whenMsg stepFail -> handleStepFail 

	State handleStepOk{	
		delayVar PauseTime	
	}
	Goto next
	
	State handleStepFail{ 
		println("Fail step :(")
			onMsg( stepFail:stepFail(R,D) ) {
				["Tback=payloadArg(1).toString().toLong()  
				 if( Tback > StepTime * 2 / 3 ) Tback = 0 else Tback=Tback/2
				"]
					if "Tback > 0 "{
				 		forward mindrobot -m local_modelChanged : local_modelChanged(robot,s)  
				 		delayVar Tback 
				  		forward mindrobot -m local_modelChanged : local_modelChanged(robot,h) 
					}
			}
			
	}
	Goto next

	
}



//Application move step
QActor onecellforward context ctxController {
	["var foundObstacle = false; var StepTime = 0L"]  
	State s0 initial {	   
		["foundObstacle = false "]
	} 
	Transition t0 whenMsg onestep -> doMoveForward
	
	State doMoveForward{		  
		
		onMsg( onestep : onestep( TIME ) ) {
			["StepTime = payloadArg(0).toLong()"]
			forward mindrobot -m local_modelChanged : modelChanged(  robot, w)
		}
	} 
	Transition t0 whenTimeVar  StepTime -> endDoMoveForward		
 		          whenEvent sonarRobot  -> handleSonarRobot
		  
	State endDoMoveForward{
		forward mindrobot -m local_modelChanged : modelChanged(robot,h)
		forward controller -m stepOk : stepOk(ok)  //answer
	}
	Goto s0
	
	//Substitute with an ad-hoc actor for a real robot
	State handleSonarRobot{  //before than the step time
		printCurrentMessage
 		onMsg ( sonarRobot : sonar( DISTANCE ) ){     
			["val distance = Integer.parseInt( payloadArg(0) ) 
              foundObstacle = (distance<20) "]  
   		}
	}
	
	Goto stepFail if "foundObstacle" else s0
	
	State stepFail{
		println("Actor: OneStepForward; State:stepfail ")
		solve( wduration( TIME ) )
		forward controller -m stepFail : stepFail(obstacle,#TIME)  	
	}
	Goto s0
}